/*
 * Copyright 1/1/18 7:29 PM randhirgupta
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'buildsystem/dependencies.gradle'
apply plugin: 'kotlin'
apply plugin: 'kotlin-android-extensions'

buildscript {

    ext.gradleVersion = '3.0.1'
    ext.kotlin_version = '1.2.21'
    ext.butterKnifeVersion = '8.7.0'

    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath "com.jakewharton:butterknife-gradle-plugin:$butterKnifeVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}


ext {
    androidApplicationId = "com.randhir.musicplayer"
    androidVersionCode = 1
    androidVersionName = "1.0"

    dataLibraryVersionCode = 1
    dataLibraryVersionName = "1.0"

    domainLibraryVersionCode = 1
    domainLibraryVersionName = "1.0"

    testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
    testApplicationId = "com.randhir.musicplayer" + '.test'
}

task runUnitTests(dependsOn: [':data:testDebugUnitTest', ':domain:testDebugUnitTest', ':app:testDebugUnitTest']) {
    description 'Run all unit tests'
}

task runAcceptanceTests(dependsOn: [':app:connectedAndroidTest']) {
    description 'Run all acceptance tests.'
}

task deployDebug(type: Exec, dependsOn: 'app:installDebug') {
    def rootDir = project.rootDir
    def localProperties = new File(rootDir, "local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream {
            inputStream -> properties.load(inputStream)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        def adb = "$sdkDir/platform-tools/adb"
        commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.randhir.musicplayer/com.randhir.musicplayer.presentation.ui.activities.MainActivity'
    }
}